def promote{fn} = {
  def fn{a:T,b & match{'number',kind{b}}} = fn{a , cast{T,b}}
  def fn{a,b:T & match{'number',kind{a}}} = fn{cast{T,a} , b}
}
def arith{fn, op} = {
  def fn{a:T,b:T} = emit{T, op, a, b}
  promote{fn}
}
def compare{fn, op} = {
  def fn{a:T,b:T} = emit{u1, op, a, b}
  promote{fn}
}

def __pnt{a:T & match{'pointer',typekind{T}}} = load{a,0}

def arith1{fn, op} = { def fn{a:T} = emit{T, op, a}; fn }
def __neg = arith1{__neg, '-'}

def __add = arith{__add, 'op +'}
def __sub = arith{__sub, 'op -'}
def __mul = arith{__mul, 'op *'}
def __div = arith{__div, 'op /'}

def __add{a,b:P & match{'pointer',typekind{P}}}  = emit{P, 'op +', cast{u64,a}, b}
def __add{a:P,b & match{'pointer',typekind{P}}}  = emit{P, 'op +', a, cast{u64,b}}
def __sub{a:P,b & match{'pointer',typekind{P}}}  = emit{P, 'op -', a, cast{u64,b}}
def __sub{a:P,b:P & match{'pointer',typekind{P}}}  = emit{u64, 'op -', a, b}

def __eq  = compare{__eq, 'op =='}
def __ne  = compare{__ne, 'op !='}
def __gt  = compare{__gt, 'op >' }
def __ge  = compare{__ge, 'op >='}
def __lt  = compare{__lt, 'op <' }
def __le  = compare{__le, 'op <='}

def logic = arith
def __and = logic{__and, 'op &'}
def __or  = logic{__or , 'op |'}
def __xor = logic{__xor, 'op ^'}
def logic1 = arith1

def __not = logic1{__not, '~'}
def __not{a:u1} = emit{u1, '!', a}

def __shl = logic{__shl, 'op <<'}
def __shr = logic{__shr, 'op >>'}

def cast_p{T, x} = emit{T, '(void*)', x}
def cast_i{T, x} = emit{T, '', x}
def assert{X} = emit{void, 'si_assert', X}
